PAGE 0
;$+MACRO

;		EQUATES USED BY FOLLOWING MACROS
;
;		DEFINED WITH "SET" TO KEEP THEM OUT OF ".SYM" FILE
;		--- THE VALUES ARE NOT CHANGED
;

POW0	SET		1
POW1	SET		2
POW2	SET		4
POW3	SET		8
POW4	SET		10H
POW5	SET		20H
POW6	SET		40H
POW7	SET		80H
POW8	SET		100H

ALSET1	SET		080H
ALSET2	SET		0C0H
ALSET3	SET		0E0H
ALSET4	SET		0F0H
ALSET5	SET		0F8H
ALSET6	SET		0FCH
ALSET7	SET		0FEH
ALSET8	SET		0FFH
ALSET9	SET		080FFH
ALSET10	SET		0C0FFH
ALSET11	SET		0E0FFH
ALSET12	SET		0F0FFH
ALSET13	SET		0F8FFH
ALSET14	SET		0FCFFH
ALSET15	SET		0FEFFH
ALSET16	SET		0FFFFH


SD		SET		0			;SINGLE DENS (FM) CODE
DD		SET		40H			;DOUBLE DENS (MFM) CODE
TRACKS8	SET		77
TRACKS5	SET		40

FDREL	MACRO	INCHES,DENS,SLEN,SPT,GAPLRW,GAPLFM,SKEW,SINGSIDE,DOUBSIDE
		DB		INCHES+DENS,SLEN,SPT,GAPLRW,GAPLFM,SKEW
;;		DB		INCHES+DENS				;INCHES+DENS
;;		DB		SLEN					;SLEN: 0=128, 1=245, 2=512, 3=1024
;;		DB		SPT					;SECTORS PER TRACK
;;		DB		GAP1RW				;GAP 1 LENGTH FOR NEC765 READ/WRITE CMDS
;;		DB		GAP1FM				;GAP 1 LENGTH FOR NEC765 FORMAT TRACK CMD
;;		DB		SKEW					;SKEW FACTOR
SECTPT	SET		SPT
;; SET TRKLEN TO # OF 128-BLOCKS / TRACK
		SETPOW	LSPERPS,%SLEN		;LOGICAL SECTORS PER PHYSICAL SECTOR
TRKLEN	SET		LSPERPS*SECTPT
TRKS	SET		TRACKS&INCHES		;KK ADDED TRKS TO DOUBLE CAPACITY ON 2 SIDED
		FDREL1	INCHES,SINGSIDE

;;SECTPT	SET		SECTPT+SECTPT	;;KK DON'T DOUBLE
;;TRKLEN	SET		TRKLEN+TRKLEN	;;KK DTRKLEN IS NOT CYLINDER LENGTH
TRKS	SET		TRKS+TRKS			;;KK DOUBLE THIS INSTEAD
		FDREL1	INCHES,DOUBSIDE
		ENDM


FDREL1	MACRO	INCHES,TOFFSET,BSHIFTF,DIRBKS,DIRENTS,MSDSAL,MSDRDE,MSDMBY
		SETPOW	BSHIFTM,%BSHIFTF
;;DSM	SET	(TRKLEN*(TRACKS&INCHES-TOFFSET)/BSHIFTM)-1	;KK MODIFIED AS
DSM		SET	(TRKLEN*(TRKS-TOFFSET)/BSHIFTM)-1			;KK ...THIS LINE
;; MAKE NUMBER OF DIRECTORY ENTRIES FROM DIRBKS OR DIRENTS
DIRE	SET		0
		IF NOT NUL DIRBKS
DIRE	SET		(DIRBKS)*4*BSHIFTM
		ENDIF
		IF NOT NUL DIRENTS
DIRE	SET		DIRENTS
		ENDIF
;; NOW REVERSE ENGINEER TO GET DIRECTORY BLOCKS
DIRB	SET		((DIRE+3)/4+BSHIFTM-1)/BSHIFTM
		IF NOT NUL DIRBKS
		IF DIRBKS > DIRB
DIRB	SET		DIRBKS
		ENDIF
		ENDIF
		IF DSM < 256
		SETPOW	EXM,%BSHIFTF-3
		ELSE
		SETPOW	EXM,%BSHIFTF-4
		ENDIF
		SETAL01	%DIRB
		DB		TRKLEN,0,BSHIFTF,BSHIFTM-1,EXM-1
;;		DB		TRKLEN				;TRKLEN
;;		DB		0					;0
;;		DB		BSHIFTF					;BSHIFTF
;;		DB		BSHIFTM-1			;BSHIFTM-1
;;		DB		EXM-1				;EXM-1

;;		DW		DSM,DIRE-1,AL01,(DIRE+3)/4,TOFFSET,0	;KK MAKES TOO BIG
														;KK ..CKS
CKSIZE	SET		(DIRE+3)/4								;KK
		IF	INCHES EQ 5									;KK
CKMAX	SET		30H										;KK
		ELSE											;KK
CKMAX	SET		20H										;KK
		ENDIF
		IF	CKSIZE GT CKMAX								;KK
CKSIZE	SET		CKMAX									;KK
		ENDIF											;KK
		DW		DSM,DIRE-1,AL01,CKSIZE,TOFFSET,0		;;KK
;;		DW		DSM					;DSM
;;		DW		DIRE-1				;DIRRECTORY ENTRIES
;;		DW		AL01				;ALLOCATION
;;		DW		CKSIZE				;CHECK SIZE
;;		DW		TOFFSET					;TRACKS OFFSET
;;		DW		0					;0

; NOTE: DUMMY CP/M-3 PSH AND PHM FOLLOWS TRACK OFFSET
; BPB FOLLOWS...
TOTSECT	SET		SECTPT*TRKS			;TOTAL NUMBER OF SECTORS IN MEDIA
TOTALOC	SET		TOTSECT/MSDSAL		;TOTAL ALLOCATION UNITS IN MEDIA
SLEN	SET		LSPERPS*128			;SECTOR LENGTH IN BYTES
CFS		SET		(TOTALOC+TOTALOC+TOTALOC+SLEN+SLEN-1)/(SLEN+SLEN)
;;		DW		SLEN,MSDSAL,1,0,2,MSDRDE,TOTSECT,MSDMBY,CFS,SECTPT
		DW		SLEN				;BYTES/SECTOR
		DB		MSDSAL,1,0,2				;SECTS/ALLOC, RESVD SECTS, NUMBERFATS
		DW		MSDRDE,TOTSECT			;ROOT DIR ENTS, TOTAL SECTORS IN MEDIA
		DB		MSDMBY				;MEDIA BYTE
		DW		CFS,SECTPT			;CALCULATED SECTORS PER FAT, SECTORS PER TRACK
		ENDM

SETPOW	MACRO	RESULT,ARG
RESULT	SET		POW&ARG
		ENDM

SETAL01	MACRO	ARG
AL01	SET		ALSET&ARG
		ENDM

;
;		RELATIONSHIP BETWEEN --
;
;		DISK TYPE			GAP LENGTH 1
;		SECTOR LENGTH		GAP LENGTH 2
;		NUMBER OF SECTORS	SKEW FACTOR
;
;		PLUS FOR CP/M ONE AND TWO SIDED DPB'S
;
;		DPB PARAMETERS ARE <ONE SIDE>,<TWO SIDES>
;		WITHIN THE POINTY BRACKETS ARE ---
;
;	P1	TRACK OFFSET (IS THIS EVER NOT = 2?)
;	P2	CP/M ALLOCATION UNIT SIZE (3=1K, 4=2K, 5=4K, 6=8K, 7=16K)
;	P3	ALLOCATION UNITS RESERVER FOR DIRECTORY
;	P4	MAXIMUM NUMBER OF FILES ALLOWED (IF NULL THEN COMPUTE FROM P3)
;
;		AND THEN FOR MS-DOS...
;
;	P5	(PHYSICAL) SECTORS/ALLOCATION UNIT
;	P6	NUMBER OF ROOT DIRECTORY ENTRIES
;	P7	MEDIA BYTE
;		SECTORS REQUIRED TO HOLD FAT IS GENERATED IN MACRO
;
;		ALL PARAMETERS ARE REQUIRED EXCEPT ONLY ONE OF P3/P4
;		NEED BE PRESENT.  IF BOTH P3 AND P4 ARE PRESENT, P3 IS
;		ONLY USED IF IT IS LARGER THAN NEEDED.
;
;	TYPE:
;		05H	5" SINGLE DENSITY (FM)
;		08H	8" SINGLE DENSITY (FM)
;		45H	5" DOUBLE DENSITY (MFM)
;		48H	8" DOUBLE DENSITY (MFM)
;
;	SECTOR LENGTH
;		0	128
;		1	256
;		2	512
;		3	1024
;
;	NUMBER OF SECTORS PER TRACK:
;		SECTORS PER SIDE IF DOUBLE SIDED
;
;	GAP LEN1:
;		GAP LENGTH TO BE USED WITH NEC765 (INTEL 8272) READ/WRITE CMDS
;
;	GAP LEN2:
;		GAP LENGTH TO BE USED WITH NEC765 FORMAT TRACK COMMAND
;
;	SKEW FACTOR:
;		LOGICALY CONTIEGEOUS SECTORS ARE THIS FAR APART
;
;KK CP/M TOFFSET DOUBLED ON DOUBLE SIDED FLOPPIES

FDRELTAB:
STD8:
 FDREL 8,SD,0,26,007H,01BH,6,<2,3,2,,4,068,002H>,<4,4,2,,4,068,003H>;8" SD 128
LENFDREL	EQU		$-FDRELTAB
 FDREL 8,SD,1,15,00EH,02AH,1,<2,4,2,,2,112,004H>,<4,4,2,,2,112,005H>;8" SD 256
 FDREL 8,SD,2,08,01BH,03AH,1,<2,4,2,,2,112,006H>,<4,4,2,,2,112,007H>;8" SD 512
 FDREL 8,SD,3,04,047H,08AH,1,<2,4,2,,2,112,008H>,<4,4,2,,2,112,009H>;8" SD 1024
 
 FDREL 8,DD,1,26,00EH,036H,3,<2,4,2,,2,112,00AH>,<4,4,2,,2,112,00BH>;8" DD 256
 FDREL 8,DD,2,15,01BH,054H,1,<2,4,2,,2,112,00CH>,<4,4,2,,2,112,00DH>;8" DD 512
 FDREL 8,DD,3,08,035H,074H,2,<2,4,2,,1,112,00EH>,<4,4,2,,1,192,00FH>;8" DD 1024 W/W
NFMT8		EQU		($-STD8)/LENFDREL

STD5:
 FDREL 5,SD,0,16,010H,019H,1,<2,3,2,,1,112,018H>,<4,3,2,,2,112,019H>;5" SD 128 16 SECT
 FDREL 5,SD,0,17,007H,009H,1,<2,3,2,,1,112,01AH>,<4,3,2,,2,112,01BH>;5" SD 128 **NEW**
 FDREL 5,SD,0,18,007H,009H,5,<3,3,,32,1,112,01CH>,<4,3,2,,2,112,01DH>;5" SD 128 Xerox
 FDREL 5,SD,1,08,018H,030H,1,<2,3,2,,1,112,01EH>,<4,3,2,,2,112,01FH>;5" SD 256
 FDREL 5,SD,1,10,007H,010H,2,<3,4,,64,1,112,020H>,<4,3,2,,2,112,021H>;5" SD 256 Osborne
 FDREL 5,SD,2,04,046H,087H,1,<2,3,2,,1,112,022H>,<4,3,2,,2,112,023H>;5" SD 512

 FDREL 5,DD,1,16,020H,032H,1,<2,3,,64,1,112,024H>,<2,4,,256,2,112,025H>;5" DD 256 16 SECT
 FDREL 5,DD,1,17,00AH,00CH,1,<3,3,,64,1,112,026H>,<6,4,,128,2,112,027H>;5" DD 256 **NEW**
 FDREL 5,DD,1,18,00AH,00CH,1,<2,3,2,,1,112,028H>,<4,4,,64,2,112,029H>;5" DD 256 18 SECT
 FDREL 5,DD,2,08,02AH,050H,1,<1,3,,64,1,64,0FEH>,<2,4,,64,2,112,0FFH>;5" DD 512 PC 8 SECT
 FDREL 5,DD,2,09,0030,0060,1,<2,3,,64,1,64,0FCH>,<4,4,2,,2,112,0FDH>;5" DD 512 PC 9 SECT
 FDREL 5,DD,2,10,0017,0034,4,<2,3,,64,1,64,0FAH>,<4,4,,128,2,112,0FBH>;5" DD 512 10 SECT
 FDREL 5,DD,3,04,080H,0F0H,1,<2,3,2,,1,112,030H>,<4,4,2,,2,112,031H>;5" DD 1024 4 SECT
 FDREL 5,DD,3,05,0028,0050,3,<2,4,,128,1,112,032H>,<4,4,,192,2,112,033H>;5" DD 1024 **NEW**
NFMT5		EQU		($-STD5)/LENFDREL
			DB		0FFH			;END OF LIST
